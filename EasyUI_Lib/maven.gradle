
// 发布需要的插件

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
// 项目引用的版本号，比如compile 'com.hsy.utils:utilslibrary:1.0.1'中的1.0.1就是这里配置的。
version = "0.0.4"
// 定义两个链接，下面会用到。
def siteUrl = 'https://github.com/shuqingcc/EasyUI' // 项目主页。
def gitUrl = 'https://github.com/shuqingcc/EasyUI.git' // Git仓库的url。https://github.com/huangshuyuan/UtilsDemo.git
// 唯一包名，比如compile 'com.hsy.utils:utilslibrary:1.0.1'中的com.hsy.utils就是这里配置的。
// :utilslibrary后面会根据项目名称得到
group = "com.qinssen.easyui"
install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'EasyUI'// 可选，项目名称。
                description 'Android Utils'// 可选，项目描述
                url siteUrl// 项目主页，这里是引用上面定义好。

                // 软件开源协议，现在一般都是Apache License2.0吧，复制我的，这里不需要修改。
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //填写开发者基本信息，复制我的，这里需要修改。
                developers {
                    developer {
                        id 'qinssen' // 开发者的id。
                        name 'qinssen' // 开发者名字。
                        email 'shuqingcc@sina.cn'// 开发者邮箱。
                    }
                }
                // SCM，复制我的，这里不需要修改。
                scm {
                    connection gitUrl// Git仓库地址。
                    developerConnection gitUrl// Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}
// 生成jar包的task，不需要修改。
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false// 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}
// 生成javaDoc的jar，不需要修改。
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，
// 所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")// Bintray的用户名。
    key = properties.getProperty("bintray.apikey")// Bintray刚才保存的ApiKey。

    configurations = ['archives']
    pkg {
        repo = "EasyUI"// 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字，如何创建请看上方。）
        name = "EasyUI"// 发布到Bintray上的项目名字，这里的名字不是compile 'com.hsy.utils:utilslibrary:1.0.1'中的utilslibrary。
        userOrg = 'shuqingcc'//// Bintray的用户名。
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true//是否公开项目
//        version {
//            gpg {
//                sign = true
//                passphrase = properties.getProperty("bintray.gpg.password")
//            }
//        }
    }
}

javadoc { //javadoc 采用 utf-8 编码否则会报“GBK的不可映射字符”错误
    options{
        encoding "UTF-8"
        charSet "UTF-8"
    }
}

